;[101]能力の表示以下の処理

;-----------------------------------------------
@CHARA_INFO
#DIM TMP_TARGET
#DIM SORT_SELECT
;-----------------------------------------------
;キャラ情報一覧の処理。ここ→個別キャラ→コマンドと選択していく。
;関数USERSHOPからで呼ばれる。
;リターン1すると戻った先でターン終了する。

SORT_SELECT = 101

$SELECT_LOOP

;キャラの一覧表示
IF SORT_SELECT == 101
	CALL SHOW_CHARA_INFO_LIST
ELSEIF SORT_SELECT == 102
	CALL SHOW_CHARA_MONEY_LIST
ELSEIF SORT_SELECT == 103
	CALL SHOW_CHARA_DEBT_LIST
ELSE
	CALL SHOW_CHARA_INFO_LIST
ENDIF
PRINTL [100] 돌아온다 [101] 번호순 [102] 소지금순 [103] 부채순
DRAWLINE

;入力
$INPUT_LOOP3
INPUT

IF RESULT > 100 && RESULT < 104
	;ランキング入力の場合、並び直して再表示
	SORT_SELECT = RESULT
	GOTO SELECT_LOOP
;無効な入力を弾く
ELSEIF (RESULT < 0 || RESULT >= CHARANUM) && (RESULT != 100)
	GOTO INPUT_LOOP3
ENDIF

;有効な入力
IF RESULT == 100
	;돌아온다
	RETURN 0
ELSE
	;個別情報表示へ
	CALL CHARA_INFO_INDIVIDUAL(RESULT)
	IF RESULT == 1
		;もし1が返って来たらターン終了の目印
		;それ以外は再び一覧表示する
		RETURN 1
	ENDIF
ENDIF
RESTART

;-----------------------------------------------
@SHOW_CHARA_INFO_LIST
#DIM MAX_NUM_LEN = 0
#DIM MAX_NAME_LEN = 0
#DIM MAX_LV_LEN = 0
#DIM MAX_ATK_LEN = 0
#DIM MAX_DEF_LEN = 0
;-----------------------------------------------
;存在するキャラの一覧表示
;全員出してるので表示人数は{CHARANUM}人となる

;キャラの番号、名前の文字数、レベル、攻撃、防御の最大桁数をそれぞれ取得
MAX_NUM_LEN = STRLENS(TOSTR(CHARANUM));番号
FOR COUNT, 0, CHARANUM
	MAX_NAME_LEN = MAX(STRLENS(SAVESTR:COUNT), MAX_NAME_LEN);名前
	MAX_LV_LEN = MAX(STRLENS(TOSTR(CFLAG:COUNT:9)), MAX_LV_LEN);レベル
	MAX_ATK_LEN = MAX(STRLENS(TOSTR(CFLAG:COUNT:13)), MAX_ATK_LEN);攻撃
	MAX_DEF_LEN = MAX (STRLENS(TOSTR(CFLAG:COUNT:14)), MAX_DEF_LEN);防御
NEXT

;1列ずつ表示
;まおーさま
PRINTFORML %"[0]", MAX_NUM_LEN+2, RIGHT% 　　　　 %NAME:0, MAX_NAME_LEN,LEFT% {CFLAG:0:9, MAX_LV_LEN,RIGHT}LV
;他の人たち
FOR COUNT, 1, CHARANUM
	;番号
	LOCALS = [{COUNT}]
	PRINTFORM %LOCALS, MAX_NUM_LEN + 2, RIGHT% 
	;行動状態
	CALL SHOW_CHARA_ACT(COUNT)
	;名前、レベル
	PRINTFORM  %SAVESTR:COUNT,MAX_NAME_LEN,LEFT% {CFLAG:COUNT:9,MAX_LV_LEN,RIGHT}LV

	;攻撃力/防御力、カルマ
	PRINTFORM  공격{CFLAG:COUNT:13,MAX_ATK_LEN,LEFT}/방어{CFLAG:COUNT:14,MAX_DEF_LEN,LEFT} 카르마{CFLAG:COUNT:151,4,RIGHT}
	;体力・気力バー
	PRINTFORM  HP%BARSTR(BASE:COUNT:0,MAXBASE:COUNT:0,8)% 기력%BARSTR(BASE:COUNT:1,MAXBASE:COUNT:1,8)%
	;애정、음란
	IF TALENT:COUNT:애정
		SETCOLOR 255,100,100
		PRINT <애정>
		RESETCOLOR
	ELSEIF TALENT:COUNT:음란
		SETCOLOR 255,100,100
		PRINT <음란>
		RESETCOLOR
	ELSE
		SETCOLOR 100,100,100
		PRINT <미함락>
		RESETCOLOR
	ENDIF
	;パーティフラグ
	;パーティメンバー
	IF CFLAG:COUNT:533 > 0 && CFLAG:COUNT:531 == 0 && CFLAG:COUNT:532 == 0 && CFLAG:COUNT:533 != COUNT
		SETCOLOR 255,100,100
		PRINTFORM  PM<{CFLAG:COUNT:533}>
		RESETCOLOR
	;パーティリーダー
	ELSEIF CFLAG:COUNT:533 > 0
		SETCOLOR 255,100,100
		PRINTFORM  PL<{CFLAG:COUNT:533}>
		RESETCOLOR
	ENDIF
	;帰還フラグ
	IF CFLAG:COUNT:507 == 1
		SETCOLOR 200,200,100
		PRINTFORM 《귀환중》
		RESETCOLOR
	ENDIF
	;お気に入り
	IF CFLAG:COUNT:700
		PRINTFORM [☆]
	ENDIF
	PRINTL 
NEXT

;-----------------------------------------------
@SHOW_CHARA_MONEY_LIST
#DIM MAX_NUM_LEN = 0
#DIM MAX_NAME_LEN = 0
#DIM MAX_RACE_LEN = 0
#DIM MAX_MIND_LEN = 0
#DIM MAX_LV_LEN = 0
#DIM MAX_MONEY_LEN = 0
#DIM MAX_MONEY
#DIM MONEY_ID
#DIM MONEY_SORT,1000
#DIM RANK_COUNT
;-----------------------------------------------
;存在するキャラの所持金順表示
;全員出してるので表示人数は{CHARANUM}人となる

;キャラの番号、名前の文字数、レベル、所持金の最大桁数をそれぞれ取得
MAX_NUM_LEN = STRLENS(TOSTR(CHARANUM));番号
FOR COUNT, 0, CHARANUM
	MAX_NAME_LEN = MAX(STRLENS(SAVESTR:COUNT), MAX_NAME_LEN);名前
	MAX_LV_LEN = MAX(STRLENS(TOSTR(CFLAG:COUNT:9)), MAX_LV_LEN);レベル
	MAX_MONEY_LEN = MAX(STRLENS(TOSTR(CFLAG:COUNT:580)), MAX_MONEY_LEN);소지금
	MAX_RACE_LEN = MAX(STRLENS(GET_LOOK_INFO(COUNT, "종족")), MAX_RACE_LEN);종족
	MAX_MIND_LEN = MAX(STRLENS(GET_LOOK_INFO(COUNT, "성격")), MAX_MIND_LEN);성격
NEXT

;ソート
;MAX_MONEY = 最大値
MAX_MONEY = -9999

;最大値を算出
FOR MONEY_ID, 1, CHARANUM
	MAX_MONEY = MAX(CFLAG:MONEY_ID:580, MAX_MONEY);소지금
NEXT

;無限ループ避け
LOCAL:1 = 0

;最大値の次に大きい数記憶
LOCAL:2 = -9999

RANK_COUNT = 0

WHILE RANK_COUNT < CHARANUM
	
	FOR MONEY_ID, 0, CHARANUM
		
		;現在の最大値と同じものを選択
		IF MAX_MONEY == CFLAG:MONEY_ID:580
			
			;リストにIDを記憶
			MONEY_SORT:RANK_COUNT = MONEY_ID
			;順位を進める
			RANK_COUNT++
			
		ELSEIF CFLAG:MONEY_ID:580 < MAX_MONEY && CFLAG:MONEY_ID:580 > LOCAL:2
			
			;現在の最大値ではない、現在の最大値の次に大きい数記憶
			LOCAL:2 = CFLAG:MONEY_ID:580
			
		ENDIF
		
	NEXT
	
	;次にソートする最大値をセット
	MAX_MONEY = LOCAL:2
	LOCAL:2 = -9999
	
	;無限ループ避け
	LOCAL:1++
	SIF LOCAL:1 > 1000
		BREAK
	
WEND


;1列ずつ表示
;まおーさま
PRINTFORML %"[0]", MAX_NUM_LEN+2, RIGHT% 　　　　 %NAME:0, MAX_NAME_LEN,LEFT% {CFLAG:0:9, MAX_LV_LEN,RIGHT}LV
;他の人たち
FOR RANK_COUNT, 0, CHARANUM
	;順位順に引き出す
	COUNT = MONEY_SORT:RANK_COUNT
	;魔王様の重複表示を避ける
	;逆に表示したい時はコメントアウトする
	SIF COUNT == 0
		CONTINUE
	
	;番号
	LOCALS = [{COUNT}]
	PRINTFORM %LOCALS, MAX_NUM_LEN + 2, RIGHT% 
	;行動状態
	CALL SHOW_CHARA_ACT(COUNT)
	;名前、レベル
	PRINTFORM  %SAVESTR:COUNT,MAX_NAME_LEN,LEFT% {CFLAG:COUNT:9,MAX_LV_LEN,RIGHT}LV
	
	;種族と성격
	PRINTFORM  %GET_LOOK_INFO(COUNT, "종족"),MAX_RACE_LEN,LEFT% - %GET_LOOK_INFO(COUNT, "성격"),MAX_MIND_LEN,LEFT%
	
	;소지금
	PRINTFORM  소지금{CFLAG:COUNT:580,MAX_MONEY_LEN,RIGHT} 
	
	;애정、음란
	IF TALENT:COUNT:애정
		SETCOLOR 255,100,100
		PRINT <애정>
		RESETCOLOR
	ELSEIF TALENT:COUNT:음란
		SETCOLOR 255,100,100
		PRINT <음란>
		RESETCOLOR
	ELSE
		SETCOLOR 100,100,100
		PRINT <미함락>
		RESETCOLOR
	ENDIF
	;パーティフラグ
	;パーティメンバー
	IF CFLAG:COUNT:533 > 0 && CFLAG:COUNT:531 == 0 && CFLAG:COUNT:532 == 0 && CFLAG:COUNT:533 != COUNT
		SETCOLOR 255,100,100
		PRINTFORM  PM<{CFLAG:COUNT:533}>
		RESETCOLOR
	;パーティリーダー
	ELSEIF CFLAG:COUNT:533 > 0
		SETCOLOR 255,100,100
		PRINTFORM  PL<{CFLAG:COUNT:533}>
		RESETCOLOR
	ENDIF
	;帰還フラグ
	IF CFLAG:COUNT:507 == 1
		SETCOLOR 200,200,100
		PRINTFORM 《귀환중》
		RESETCOLOR
	ENDIF
	;お気に入り
	IF CFLAG:COUNT:700
		PRINTFORM [☆]
	ENDIF
	PRINTL 
NEXT

;-----------------------------------------------
@SHOW_CHARA_DEBT_LIST
#DIM MAX_NUM_LEN = 0
#DIM MAX_NAME_LEN = 0
#DIM MAX_LV_LEN = 0
#DIM MAX_RACE_LEN = 0
#DIM MAX_MIND_LEN = 0
#DIM MAX_DEBT_LEN = 0
#DIM MAX_DEBT
#DIM DEBT_ID
#DIM DEBT_SORT,1000
#DIM RANK_COUNT
;-----------------------------------------------
;存在するキャラの所持金順表示
;全員出してるので表示人数は{CHARANUM}人となる

;キャラの番号、名前の文字数、レベル、所持金の最大桁数をそれぞれ取得
MAX_NUM_LEN = STRLENS(TOSTR(CHARANUM));番号
FOR COUNT, 0, CHARANUM
	MAX_NAME_LEN = MAX(STRLENS(SAVESTR:COUNT), MAX_NAME_LEN);名前
	MAX_LV_LEN = MAX(STRLENS(TOSTR(CFLAG:COUNT:9)), MAX_LV_LEN);レベル
	MAX_DEBT_LEN = MAX(STRLENS(TOSTR(CFLAG:COUNT:582)), MAX_DEBT_LEN);빚
	MAX_RACE_LEN = MAX(STRLENS(GET_LOOK_INFO(COUNT, "종족")), MAX_RACE_LEN);종족
	MAX_MIND_LEN = MAX(STRLENS(GET_LOOK_INFO(COUNT, "성격")), MAX_MIND_LEN);성격
NEXT

;ソート
;MAX_DEBT = 最小値
MAX_DEBT = 9999

;最大値を算出
FOR DEBT_ID, 1, CHARANUM
	MAX_DEBT = MIN(CFLAG:DEBT_ID:582, MAX_DEBT);빚
NEXT

;無限ループ避け
LOCAL:1 = 0

;最大値の次に大きい数記憶
LOCAL:2 = 9999

RANK_COUNT = 0

WHILE RANK_COUNT < CHARANUM
	
	FOR DEBT_ID, 0, CHARANUM
		
		;現在の最小値と同じものを選択
		IF MAX_DEBT == CFLAG:DEBT_ID:582
			
			;リストにIDを記憶
			DEBT_SORT:RANK_COUNT = DEBT_ID
			;順位を進める
			RANK_COUNT++
			
		ELSEIF CFLAG:DEBT_ID:582 > MAX_DEBT && CFLAG:DEBT_ID:582 < LOCAL:2
			
			;現在の最小値ではない、現在の最小値の次に小さい数記憶
			LOCAL:2 = CFLAG:DEBT_ID:582
			
		ENDIF
		
	NEXT
	
	;次にソートする最大値をセット
	MAX_DEBT = LOCAL:2
	LOCAL:2 = 9999
	
	;無限ループ避け
	LOCAL:1++
	SIF LOCAL:1 > 1000
		BREAK
	
WEND


;1列ずつ表示
;まおーさま
PRINTFORML %"[0]", MAX_NUM_LEN+2, RIGHT% 　　　　 %NAME:0, MAX_NAME_LEN,LEFT% {CFLAG:0:9, MAX_LV_LEN,RIGHT}LV
;他の人たち
FOR RANK_COUNT, 0, CHARANUM
	;順位順に引き出す
	COUNT = DEBT_SORT:RANK_COUNT
	;魔王様の重複表示を避ける
	;逆に表示したい時はコメントアウトする
	SIF COUNT == 0
		CONTINUE
	
	;番号
	LOCALS = [{COUNT}]
	PRINTFORM %LOCALS, MAX_NUM_LEN + 2, RIGHT% 
	;行動状態
	CALL SHOW_CHARA_ACT(COUNT)
	;名前、レベル
	PRINTFORM  %SAVESTR:COUNT,MAX_NAME_LEN,LEFT% {CFLAG:COUNT:9,MAX_LV_LEN,RIGHT}LV
	
	;種族と성격
	PRINTFORM  %GET_LOOK_INFO(COUNT, "종족"),MAX_RACE_LEN,LEFT% - %GET_LOOK_INFO(COUNT, "성격"),MAX_MIND_LEN,LEFT%
	
	;소지금
	PRINTFORM  빚{CFLAG:COUNT:582,MAX_DEBT_LEN,RIGHT} 
	
	;애정、음란
	IF TALENT:COUNT:애정
		SETCOLOR 255,100,100
		PRINT <애정>
		RESETCOLOR
	ELSEIF TALENT:COUNT:음란
		SETCOLOR 255,100,100
		PRINT <음란>
		RESETCOLOR
	ELSE
		SETCOLOR 100,100,100
		PRINT <미함락>
		RESETCOLOR
	ENDIF
	;パーティフラグ
	;パーティメンバー
	IF CFLAG:COUNT:533 > 0 && CFLAG:COUNT:531 == 0 && CFLAG:COUNT:532 == 0 && CFLAG:COUNT:533 != COUNT
		SETCOLOR 255,100,100
		PRINTFORM  PM<{CFLAG:COUNT:533}>
		RESETCOLOR
	;パーティリーダー
	ELSEIF CFLAG:COUNT:533 > 0
		SETCOLOR 255,100,100
		PRINTFORM  PL<{CFLAG:COUNT:533}>
		RESETCOLOR
	ENDIF
	;帰還フラグ
	IF CFLAG:COUNT:507 == 1
		SETCOLOR 200,200,100
		PRINTFORM 《귀환중》
		RESETCOLOR
	ENDIF
	;お気に入り
	IF CFLAG:COUNT:700
		PRINTFORM [☆]
	ENDIF
	PRINTL 
NEXT


;-----------------------------------------------
@SHOW_CHARA_ACT(ARG)
;-----------------------------------------------
;ARG番のキャラの状態（侵攻中とか）を表示する
;CHARA_INFOで使用
IF CFLAG:ARG:1 == 2
	SETCOLOR 255,100,100
	PRINTFORM {CFLAG:COUNT:501}F침공중
ELSEIF CFLAG:COUNT:1 == 3
	SETCOLOR 100,255,255
	PRINTFORM {CFLAG:COUNT:501}F요격중
ELSEIF CFLAG:ARG:1 == 0
	SETCOLOR 100,100,255
	PRINT [조교가능]
ELSEIF CFLAG:ARG:1 == 7
	SETCOLOR 100,255,100
	PRINT [ 씨받이 ]
ELSEIF CFLAG:ARG:1 == 8
	SETCOLOR 100,255,100
	PRINT [노출대]
ELSEIF CFLAG:ARG:1 == 9
	SETCOLOR 255,0,0
	PRINT  [NTR중]
ELSEIF CFLAG:ARG:1 == 10
	SETCOLOR 100,255,100
	PRINT [육아실]
ELSE
	PRINT -F　―　
ENDIF
RESETCOLOR

;-----------------------------------------------
@CHARA_INFO_INDIVIDUAL(ARG)
#DIM IS_ABLE_TO_CHANGE_NAME = 0
#DIM IS_ABLE_TO_CHANGE_JOB = 0
#DIM IS_ABLE_TO_MARRIAGE = 0
#DIM IS_ABLE_TO_TEMPTATION = 0
;-----------------------------------------------
;ARG番のキャラ情報とコマンドを表示

;キャラ情報表示
CALL SHOW_CHARA_INFO(ARG)

;コマンドボタンを表示
CALL SHOW_BUTTON_NAME_EDIT(0,ARG,0) ;名前を変える
CALL SHOW_BUTTON_NAME_EDIT(1,ARG,1) ;名前を戻す
CALL SHOW_BUTTON_JOB_CHANGE(2,ARG) ;転職
CALL SHOW_BUTTON_MARRIAGE(4,ARG) ;결혼
CALL SHOW_BUTTON_TEMPTATION(3,ARG) ;魔の誘惑
CALL SHOW_BUTTON_CHILD_CARE(5,ARG) ;育児室の訪問
SIF ARG != 0
PRINTFORM [6] %\@CFLAG:ARG:700 ? 즐겨찾기 해제 # 즐겨찾기 등록 \@% 
;7番は1人称変更
CALL SHOW_BUTTON_EQUIP(8,ARG) ;装備の確認
PRINTL  
CALL SHOW_BUTTON_BICH_LEVEL(9,ARG) ;売春積極性

PRINTL [999] 돌아온다

;入力処理
$INPUT_LOOP
INPUT
;[999]ならリターン0
;それ以外は入力に対応する関数に送る
IF RESULT == 999
	RETURN 0
ELSEIF RESULT == 0
	; 名前変更
	CALL CHARA_INFO_NAME_EDIT(ARG,0)
ELSEIF RESULT == 1
	; 名前のリセット
	CALL CHARA_INFO_NAME_EDIT(ARG,1)
ELSEIF RESULT == 2
	; 転職
	CALL CHARA_INFO_JOB_CHANGE(ARG)
ELSEIF RESULT == 3
	; 魔の誘惑
	CALL TEMPTATION(ARG)
ELSEIF RESULT == 4
	; 결혼
	CALL MARRIAGE(ARG)
ELSEIF RESULT == 5
	; 育児室
	CALL CHILD_CARE_CHARA(ARG)
ELSEIF RESULT == 6
	IF ARG != 0
		CFLAG:ARG:700 = !CFLAG:ARG:700
		PRINTFORMW %\@CFLAG:ARG:700 ? 즐겨찾기에 등록했습니다.。 # 즐겨찾기에서 삭제했습니다.。 \@% 
		PRINTL
		RESULT = 0
	ENDIF
ELSEIF RESULT == 7
	CALL RANDOM_SELF_CALL, ARG
ELSEIF RESULT == 8
	CALL EQUIP_ST_SHOW, ARG
ELSEIF RESULT == 9
	CALL SET_BICH_LEVEL, ARG
ENDIF

;返り値による処理
;一応、0なら再度一覧表示、1ならターンエンド、2なら再入力となるようにしてある
IF RESULT == 0 || RESULT == 1
	RETURN RESULT
ENDIF
GOTO INPUT_LOOP


